#!/usr/bin/perl
#
# $Id: refreshmans,v 1.4 2006-01-26 18:36:17 ashted Exp $ 
#
# Rebuild the man files and stubs in the doc and doc/man_stubs directories.
#

use strict;
use warnings;

sub DOC_DIR () { 'doc'; }
sub STUB_DIR () { 'man_stubs' }

# First find out what version of the library we are running under
my $LibVer = '';
if (open(CAC,'configure.ac')) {
  while (<CAC>) {
    next unless /AC_INIT.*(\d[.0-9]+)/;
    $LibVer = $1;
  }
} else {
  warn("Unable to open configure.ac to get version number");
}

# Hello, World
my $VERSION = '$Revision: 1.4 $';
$VERSION =~ s/.*(\d.*\d).*/$1/;
print "refreshmans ver. $VERSION (library version $LibVer)\n";
print "  See makehtml and maketexi in the doc directory to build HTML\n";
print "  and/or Texinfo versions of the documentation\n\n";

# Now erase everything in the stub directory.
chdir DOC_DIR;
chdir STUB_DIR;
opendir(STUBS,'.') or
  die "Unable to open directory man_stubs for reading, stopped";
my @stubs = grep { /^[^.]/ } readdir(STUBS);
foreach (@stubs) {
  unlink $_;
}
chdir '..';

# Now find out what we have to process.
opendir(DIR,'.') or
  die "Unable to open directory . for reading: $!, stopped";
my @dir = readdir(DIR);
close DIR;
my @pods = grep { /\.pod$/ } @dir;
my %mans = map { s/.\d$//; ($_,1) } grep { /\.\d$/ } @dir;

# Now process the files.
my $contents;
POD:foreach my $pod (@pods) {
  print "Processing $pod . . .\n";

  # Take it off the list
  my $base = $pod;
  $base =~ s/.pod$//;
  delete($mans{$base});
  my $BASE = uc($base);

  my $section = 1;
  $section = 3 if $base =~ /liboctrope|octrope_link/;

  # Process it into a man file
  system("pod2man --release=\"ridgerunner$LibVer\" --section $section ".
         "--name $base --center $base $pod > $base.$section");

  # Build the stub files
  open(POD,$pod) or
    die "Unable to open file $pod for reading: $!, stopped";
  { undef local $/; $contents = <POD>; } # Suck in the entire contents
  close POD;
  my($names) = ($contents =~ /^=head1 NAME\s*(\S.+?)\s*-/ms);
  my @names = split(/,\s*/,$names);
  $names = join(', ',@names);
format STDOUT=
~~^<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
$names
.
  write;
  chdir STUB_DIR;
  $pod =~ s/.pod$/.3/;
  foreach my $name (@names) {
    $name =~ s/B<(.*)>/$1/;
    next if $pod =~ /^$name.3/;
    open(STUB,">$name.3") or
      die "Unable to open $name.3 for writing: $!, stopped";
    print STUB ".so man3/$pod\n";
    close STUB;
    chmod 0664,$name.'.3'
      or warn "Unable to change mode on $name.3: $!";
  }
  chdir '..';
}

my @not_found = keys %mans;
if (@not_found) {
  warn(join("\n   ",
       "\nWARNING!!! The following man files were found without .pods:",
       @not_found),"\n");
}
